plugins {
    id("java-library")
    id("maven-publish")
    id("fabric-loom").version("0.5-SNAPSHOT").apply(false)
}

project.group = "net.devtech"
project.version = "0.8.4"

final String minecraftVersion="20w51a"
final String yarnBuild="5"

allprojects {
    plugins.apply("fabric-loom")

    sourceCompatibility = 8
    targetCompatibility = 8

    repositories {
        mavenLocal()

        maven {url = "https://dl.bintray.com/user11681/maven"}
    }

    dependencies {
        minecraft("com.mojang:minecraft:${minecraftVersion}")
        mappings("net.fabricmc:yarn:${minecraftVersion}+build.${yarnBuild}:v2")
        modApi("net.fabricmc:fabric-loader:+")

        testImplementation("org.junit.jupiter:junit-jupiter:+")

        // concern
        // extraLibs files("${System.getProperty('java.home')}/../lib/tools.jar")

        modApi("user11681:narratoroff:+")
        modApi("user11681:noauth:+")

        modApi(include("net.bytebuddy:byte-buddy-agent:+"))
        modApi(include("net.gudenau.lib:unsafe:+"))
        modApi(include("user11681:dynamicentry:+"))
        modApi(include("user11681:reflect:+"))

//    modApi("net.fabricmc.fabric-api:fabric-api:+")
    }

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}

processResources {
    inputs.property("version", project.version)

    from(sourceSets.main.resources.srcDirs) {
        include("fabric.mod.json")
        expand("version": project.version)
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude("fabric.mod.json")
    }
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            artifact(remapJar) {
                builtBy(remapJar)
            }

            artifact(sourcesJar) {
                builtBy(remapSourcesJar)
            }
        }
    }

    repositories {
        mavenLocal()
    }
}
